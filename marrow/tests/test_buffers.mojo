import std.math as math
from std.sys import has_accelerator
from std.testing import assert_equal, assert_true, assert_false, TestSuite
from std.reflection import call_location
from std.memory import ArcPointer
from std.gpu.host import DeviceContext

from marrow.buffers import *
from marrow.bitmap import Bitmap, BitmapBuilder


@always_inline
def assert_bitmap_set(
    ptr: UnsafePointer[UInt8, MutAnyOrigin],
    n_bits: Int,
    expected_true_pos: List[Int],
    message: StringLiteral,
) -> None:
    var list_pos = 0
    for i in range(n_bits):
        var expected_value = False
        if list_pos < len(expected_true_pos):
            if expected_true_pos[list_pos] == i:
                expected_value = True
                list_pos += 1
        var current_value = Bool((ptr[i // 8] >> UInt8(i % 8)) & 1)
        assert_equal(
            current_value,
            expected_value,
            String(
                "{}: Bitmap index {} is {}, expected {} as per list position {}"
            ).format(message, i, current_value, expected_value, list_pos),
            location=call_location(),
        )


def is_aligned[
    T: AnyType
](ptr: UnsafePointer[T, MutAnyOrigin], alignment: Int) -> Bool:
    return (Int(ptr) % alignment) == 0


def test_buffer_init():
    var b = BufferBuilder.alloc(10)
    assert_equal(b.size, 64)
    assert_true(is_aligned(b.ptr, 64))


def test_alloc_bits():
    # 10 bits → ceildiv(10,8)=2 bytes → aligned to 64
    var b1 = BufferBuilder.alloc[DType.bool](10)
    assert_equal(b1.size, 64)

    # 64*8+1 bits → ceildiv(513,8)=65 bytes → aligned to 128
    var b2 = BufferBuilder.alloc[DType.bool](64 * 8 + 1)
    assert_equal(b2.size, 128)


def test_buffer_grow():
    var b = BufferBuilder.alloc(10)
    b.unsafe_set(0, 111)
    assert_equal(b.size, 64)
    b.resize(20)
    assert_equal(b.size, 64)
    assert_equal(b.unsafe_get(0), 111)
    b.resize(80)
    assert_equal(b.size, 128)
    assert_equal(b.unsafe_get(0), 111)


def test_buffer_set_get():
    var buf = BufferBuilder.alloc(10)
    assert_equal(buf.size, 64)

    buf.unsafe_set(0, 42)
    buf.unsafe_set(1, 43)
    buf.unsafe_set(2, 44)
    assert_equal(buf.unsafe_get(0), 42)
    assert_equal(buf.unsafe_get(1), 43)
    assert_equal(buf.unsafe_get(2), 44)

    assert_equal(buf.size, 64)
    assert_equal(
        buf.length[DType.uint16](), 32
    )  # 64 bytes / 2 bytes per element
    # reinterpreting the underlying bits as uint16
    assert_equal(buf.unsafe_get[DType.uint16](0), 42 + (43 << 8))
    assert_equal(buf.unsafe_get[DType.uint16](1), 44)


def test_buffer_swap():
    var one = BufferBuilder.alloc(10)
    one.unsafe_set(0, 111)
    var two = BufferBuilder.alloc(10)
    two.unsafe_set(0, 222)

    swap(one, two)

    assert_equal(one.unsafe_get(0), 222)
    assert_equal(two.unsafe_get(0), 111)


def test_bitmap_get_set():
    var b = BufferBuilder.alloc[DType.bool](10)
    assert_equal(b.size, 64)

    assert_false(Bool((b.ptr[0] >> UInt8(0)) & 1))
    b.unsafe_set[DType.bool](0, True)
    assert_true(Bool((b.ptr[0] >> UInt8(0)) & 1))
    b.unsafe_set[DType.bool](1, True)
    assert_true(Bool((b.ptr[0] >> UInt8(1)) & 1))

    var frozen = b.finish()
    assert_true(frozen.unsafe_get[DType.bool](0))
    assert_true(frozen.unsafe_get[DType.bool](1))
    assert_false(frozen.unsafe_get[DType.bool](2))
    # 10-bit bitmap → 2 set bits
    assert_equal(Bitmap(frozen, 0, 10).count_set_bits(), 2)


def _reset(mut bitmap: BitmapBuilder, n_bits: Int):
    bitmap.set_range(0, n_bits, False)
    assert_bitmap_set(bitmap.unsafe_ptr(), n_bits, [], "after _reset")


def test_bitmap_range_set():
    var bitmap = BitmapBuilder.alloc(16)
    var n_bits = 16

    bitmap.set_range(0, 10, True)
    assert_bitmap_set(
        bitmap.unsafe_ptr(),
        n_bits,
        [0, 1, 2, 3, 4, 5, 6, 7, 8, 9],
        "range 0-10",
    )
    bitmap.set_range(0, 10, False)
    assert_bitmap_set(bitmap.unsafe_ptr(), n_bits, [], "reset")

    bitmap.set_range(0, 0, True)
    assert_bitmap_set(bitmap.unsafe_ptr(), n_bits, [], "range 0")

    var to_test = [0, 1, 7, 8, 15]
    for pos in range(len(to_test)):
        _reset(bitmap, n_bits)
        var start_bit = to_test[pos]
        bitmap.set_range(start_bit, 1, True)
        assert_bitmap_set(bitmap.unsafe_ptr(), n_bits, [start_bit], "range  1")
        if to_test[pos] < n_bits - 1:
            _reset(bitmap, n_bits)
            bitmap.set_range(start_bit, 2, True)
            assert_bitmap_set(
                bitmap.unsafe_ptr(),
                n_bits,
                [start_bit, start_bit + 1],
                "range 2",
            )


def test_bitmap_extend():
    var src_b = BitmapBuilder.alloc(6)
    src_b.set_bit(0, True)
    src_b.set_bit(5, True)
    var src = src_b.finish(6)

    var dst = BitmapBuilder.alloc(8)
    dst.extend(src, 0, 6)
    assert_bitmap_set(dst.unsafe_ptr(), 8, [0, 5], "after extend")

    # extend into offset position
    var dst2 = BitmapBuilder.alloc(8)
    var src2_b = BitmapBuilder.alloc(2)
    src2_b.set_bit(0, True)
    var src2 = src2_b.finish(2)
    dst2.extend(src2, 6, 2)
    assert_bitmap_set(dst2.unsafe_ptr(), 8, [6], "extend at offset 6")


def test_buffer_finish():
    var buf = BufferBuilder.alloc(10)
    buf.unsafe_set(0, 42)
    buf.unsafe_set(1, 99)

    var frozen = buf.finish()
    # Reads still work on the frozen buffer.
    assert_equal(frozen.unsafe_get(0), 42)
    assert_equal(frozen.unsafe_get(1), 99)
    assert_equal(frozen.size, 64)
    assert_equal(frozen.length(), 64)


def test_buffer_cpu_kind():
    """CPU buffers from BufferBuilder.finish() have kind=CPU and are CPU-accessible.
    """
    var b = BufferBuilder.alloc(64)
    var frozen = b.finish()
    assert_true(frozen.is_cpu())
    assert_false(frozen.is_device())
    assert_false(frozen.is_host())
    assert_equal(frozen.device_type(), Int32(1))
    assert_equal(frozen.device_id(), Int64(-1))


def test_buffer_foreign_kind():
    """Foreign CPU buffers are CPU-accessible; release fires on last drop."""
    # n_released lives on the stack; its address is embedded in the buffer
    # data so the release callback can increment it before freeing the memory.
    var n_released: Int = 0
    var raw = alloc[UInt8](size_of[UnsafePointer[Int, MutAnyOrigin]]())
    raw.bitcast[UnsafePointer[Int, MutAnyOrigin]]()[0] = rebind[
        UnsafePointer[Int, MutAnyOrigin]
    ](UnsafePointer(to=n_released))

    fn count_and_free(ptr: UnsafePointer[UInt8, MutAnyOrigin]) -> None:
        var counter = ptr.bitcast[UnsafePointer[Int, MutAnyOrigin]]()[0]
        counter[0] += 1
        ptr.free()

    var mut_ptr = rebind[UnsafePointer[UInt8, MutAnyOrigin]](raw)
    var void_ptr = rebind[UnsafePointer[NoneType, MutAnyOrigin]](
        raw.bitcast[NoneType]()
    )
    var keeper = ArcPointer(Allocation.foreign(mut_ptr, count_and_free))
    var buf = Buffer.from_foreign(
        void_ptr, size_of[UnsafePointer[Int, MutAnyOrigin]](), keeper
    )
    assert_true(buf.is_cpu())
    assert_false(buf.is_device())
    assert_false(buf.is_host())
    assert_equal(buf.device_type(), Int32(1))
    assert_equal(buf.device_id(), Int64(-1))
    assert_equal(n_released, 0)
    # Drop both ArcPointer copies; release fires exactly once on last drop.
    _ = buf^
    _ = keeper^
    assert_equal(n_released, 1)


def test_buffer_no_device():
    # CPU-allocated buffers have no device buffer
    var buf = BufferBuilder.alloc(10)
    var frozen = buf.finish()
    assert_false(frozen.is_device())


def test_buffer_device_kind():
    """GPU DEVICE buffers are not CPU-accessible."""
    if not has_accelerator():
        return
    var ctx = DeviceContext()
    var dev = ctx.enqueue_create_buffer[DType.uint8](64)
    var buf = Buffer.from_device(dev, 64)
    assert_true(buf.is_device())
    assert_false(buf.is_cpu())
    assert_false(buf.is_host())

    # device_type inferred from context api: cuda→CUDA(2), hip→ROCM(10), metal→METAL(8)
    var api = ctx.api()
    var expected_dev_type: Int32
    if api == "cuda":
        expected_dev_type = DeviceType.CUDA
    elif api == "hip":
        expected_dev_type = DeviceType.ROCM
    else:
        expected_dev_type = DeviceType.METAL
    assert_equal(buf.device_type(), expected_dev_type)
    assert_equal(buf.device_id(), Int64(0))


def test_buffer_host_kind():
    """HOST (pinned) buffers are CPU-accessible with a valid ptr."""
    if not has_accelerator():
        return
    var ctx = DeviceContext()
    var host = ctx.enqueue_create_host_buffer[DType.uint8](64)
    var buf = Buffer.from_host(host)
    assert_true(buf.is_host())
    assert_true(buf.is_cpu())
    assert_false(buf.is_device())

    # device_type inferred from context api; raises for unrecognised APIs (e.g. metal)
    var api = ctx.api()
    if api == "cuda":
        assert_equal(buf.device_type(), DeviceType.CUDA_HOST)
    elif api == "hip":
        assert_equal(buf.device_type(), DeviceType.ROCM_HOST)
    assert_equal(buf.device_id(), Int64(0))
    # ptr is valid on CPU
    assert_true(Int(buf.ptr) != 0)


def test_buffer_host_builder():
    """BufferBuilder.alloc_host + finish produce a valid HOST buffer."""
    if not has_accelerator():
        return
    var ctx = DeviceContext()
    var b = BufferBuilder.alloc_host[DType.uint8](ctx, 64)
    b.unsafe_set(0, 7)
    b.unsafe_set(1, 13)
    var buf = b.finish()
    assert_true(buf.is_host())
    assert_true(buf.is_cpu())
    assert_false(buf.is_device())

    var api = ctx.api()
    if api == "cuda":
        assert_equal(buf.device_type(), DeviceType.CUDA_HOST)
    elif api == "hip":
        assert_equal(buf.device_type(), DeviceType.ROCM_HOST)
    assert_equal(buf.device_id(), Int64(0))
    assert_equal(buf.unsafe_get(0), UInt8(7))
    assert_equal(buf.unsafe_get(1), UInt8(13))
    # ptr must be valid (non-null) for a HOST buffer
    assert_true(Int(buf.ptr) != 0)


def test_buffer_to_cpu_round_trip():
    """Upload a CPU buffer to GPU then download back; data is preserved."""
    if not has_accelerator():
        return
    var ctx = DeviceContext()
    var builder = BufferBuilder.alloc[DType.uint8](64)
    builder.unsafe_set(0, 42)
    builder.unsafe_set(1, 99)
    var cpu_buf = builder.finish()

    var dev_buf = cpu_buf.to_device(ctx)
    assert_true(dev_buf.is_device())

    var back = dev_buf.to_cpu(ctx)
    assert_true(back.is_cpu())
    assert_equal(back.unsafe_get(0), 42)
    assert_equal(back.unsafe_get(1), 99)


def main():
    TestSuite.discover_tests[__functions_in_module()]().run()
