"""Bitmap — immutable bit-packed validity buffer with SIMD operations.

Bitmap
------
`Bitmap` wraps an immutable `Buffer` plus `_offset: Int` and `_length: Int`.

  _offset
      Bit offset into `_buffer`.  Enables zero-copy slicing: `slice(n, m)`
      returns a new `Bitmap` that shares the same `Buffer` but starts at a
      later bit position.  Always 0 for freshly-built bitmaps.

  _length
      Number of valid bits.

Copying a `Bitmap` is O(1) — it bumps the `Buffer`'s `ArcPointer` ref-count.

BitmapBuilder
-------------
Mutable counterpart.  Wraps `BufferBuilder` for incremental construction.
Call `finish(length)` to freeze into an immutable `Bitmap`.
"""

from std.sys.info import simd_byte_width, simd_width_of
from std.bit import pop_count
import std.math as math
from std.math import iota
from std.memory import bitcast, memcpy, memset

from .buffers import Buffer, BufferBuilder


# ---------------------------------------------------------------------------
# Bitmap — immutable, bit-packed
# ---------------------------------------------------------------------------


struct Bitmap(Equatable, ImplicitlyCopyable, Movable, Sized, Writable):
    """Immutable bit-packed validity bitmap.

    Wraps a `Buffer` with a `_offset` (bit offset) and `_length` (bit count).
    Copying is O(1); the backing `Buffer` uses `ArcPointer` shared ownership.
    """

    var _buffer: Buffer
    var _offset: Int
    var _length: Int

    def __init__(out self, buffer: Buffer, offset: Int, length: Int):
        self._buffer = buffer
        self._offset = offset
        self._length = length

    @always_inline
    def __len__(self) -> Int:
        return self._length

    @always_inline
    def bit_offset(self) -> Int:
        """Return the bit offset into the backing buffer."""
        return self._offset

    @always_inline
    def _aligned_byte_range(
        self,
    ) -> Tuple[UnsafePointer[UInt8, ImmutExternalOrigin], Int, Int, Int]:
        """Return 64-byte-aligned pointer and byte range with boundary bits.

        Returns (ptr, total_bytes, lead_bits, trail_bits):
          ptr:         buffer pointer backed up to the 64-byte boundary
          total_bytes: aligned_end - aligned_start (multiple of 64)
          lead_bits:   bit offset from aligned_start to first data bit
                       (lead_bytes*8 + sub-byte offset, range 0-511)
          trail_bits:  bit offset from last data bit to aligned_end
                       (trail_bytes*8 + sub-byte remainder, 0 = exact fit)

        Arrow buffers are 64-byte aligned and zero-padded, so reading the
        full [ptr, ptr + total_bytes) range is always safe.
        """
        var byte_start = self._offset >> 3
        var bit_end = self._offset + self._length
        var byte_end = (bit_end + 7) >> 3
        var aligned_start = math.align_down(byte_start, 64)
        var aligned_end = math.align_up(byte_end, 64)
        var lead_bits = self._offset - (aligned_start << 3)
        var trail_bits = (aligned_end - byte_end) * 8 + (bit_end & 7)
        return Tuple(
            self._buffer.unsafe_ptr() + aligned_start,
            aligned_end - aligned_start,
            lead_bits,
            trail_bits,
        )

    @always_inline
    def is_valid(self, index: Int) -> Bool:
        """Return True if bit at (_offset + index) is set (value is valid)."""
        var bit_index = self.bit_offset() + index
        return Bool(
            (self._buffer.ptr[bit_index >> 3] >> UInt8(bit_index & 7)) & 1
        )

    @always_inline
    def is_null(self, index: Int) -> Bool:
        """Return True if bit at (_offset + index) is unset (value is null)."""
        return not self.is_valid(index)

    @always_inline
    def mask[dtype: DType, W: Int](self, abs_pos: Int) -> SIMD[DType.bool, W]:
        """Expand W consecutive bitmap bits starting at abs_pos into a SIMD bool vector.

        Each lane j of the result is True iff bit (abs_pos + j) is set in the bitmap.
        Loads a full UInt32 unconditionally — safe because Arrow buffers are always
        64-byte padded, so reading 4 bytes at any valid byte offset never faults.
        """
        var bp = self._buffer.unsafe_ptr()
        var byte_idx = abs_pos >> 3
        var bit_off = abs_pos & 7

        # Single unaligned 4-byte load: branchless, safe due to 64-byte padding.
        var bits = (bp + byte_idx).bitcast[UInt32]().load[alignment=1]()
        bits >>= UInt32(bit_off)

        return (
            (SIMD[DType.uint32, W](bits) >> iota[DType.uint32, W]()) & 1
        ).cast[DType.bool]()

    def any_set(self) -> Bool:
        """Return True if any bit in [_offset, _offset + _length) is set.

        Early-exits on the first non-zero SIMD chunk. Boundary bytes are
        masked to exclude bits outside the logical range. No scalar tail
        is needed: Arrow buffers are 64-byte padded so SIMD overreads are safe.
        """
        if self._length == 0:
            return False

        comptime width = simd_width_of[DType.uint8]()
        var ptr = self._buffer.unsafe_ptr()
        var bit_start = self._offset
        var bit_end = bit_start + self._length
        var byte_start = bit_start >> 3
        var byte_end = (bit_end + 7) >> 3
        var nbytes = byte_end - byte_start

        # Mask out bits below _offset in the first byte and above
        # _offset+_length in the last byte so they don't affect the result.
        var first_mask = UInt8(0xFF) << UInt8(bit_start & 7)
        var last_mask = UInt8(
            (1 << ((bit_end - 1) & 7) + 1) - 1
        ) if bit_end & 7 != 0 else UInt8(0xFF)

        if nbytes == 1:
            return (ptr[byte_start] & first_mask & last_mask) != 0

        # Check first and last boundary bytes.
        if (ptr[byte_start] & first_mask) != 0:
            return True
        if (ptr[byte_end - 1] & last_mask) != 0:
            return True

        # SIMD scan of middle bytes — safe to overread due to 64-byte padding.
        var i = byte_start + 1
        var end = byte_end - 1
        while i + width <= end:
            if (ptr + i).load[width=width]().reduce_or() != 0:
                return True
            i += width
        # Remaining middle bytes (< width).
        while i < end:
            if ptr[i] != 0:
                return True
            i += 1

        return False

    def all_set(self) -> Bool:
        """Return True if all bits in [_offset, _offset + _length) are set.

        Early-exits on the first non-0xFF SIMD chunk. Boundary bytes are
        masked to exclude bits outside the logical range. No scalar tail
        is needed: Arrow buffers are 64-byte padded so SIMD overreads are safe.
        """
        if self._length == 0:
            return True

        comptime width = simd_width_of[DType.uint8]()
        var ptr = self._buffer.unsafe_ptr()
        var bit_start = self._offset
        var bit_end = bit_start + self._length
        var byte_start = bit_start >> 3
        var byte_end = (bit_end + 7) >> 3
        var nbytes = byte_end - byte_start

        # Mask: set boundary bits outside the logical range to 1 so they
        # don't cause a false negative (we're checking all-ones).
        var first_fill = ~(UInt8(0xFF) << UInt8(bit_start & 7))
        var last_fill = ~(
            UInt8((1 << ((bit_end - 1) & 7) + 1) - 1)
        ) if bit_end & 7 != 0 else UInt8(0)

        if nbytes == 1:
            return (ptr[byte_start] | first_fill | last_fill) == 0xFF

        # Check first and last boundary bytes.
        if (ptr[byte_start] | first_fill) != 0xFF:
            return False
        if (ptr[byte_end - 1] | last_fill) != 0xFF:
            return False

        # SIMD scan of middle bytes — safe to overread due to 64-byte padding.
        var i = byte_start + 1
        var end = byte_end - 1
        while i + width <= end:
            if (ptr + i).load[width=width]().reduce_and() != 0xFF:
                return False
            i += width
        # Remaining middle bytes (< width).
        while i < end:
            if ptr[i] != 0xFF:
                return False
            i += 1

        return True

    def count_set_bits_with_range(self) -> Tuple[Int, Int, Int]:
        """Count set bits and return the logical bit range that covers them.

        Same SIMD scan as count_set_bits(), but also tracks the first and last
        512-byte (Tier 1) or 64-byte (Tier 2) block that contains set bits.
        Callers can use the returned range to bound filter loops, skipping
        leading and trailing zero blocks without an extra pass.

        Lead/trail bytes are zero per the Arrow buffer spec, so block-level
        tracking is accurate regardless of the bitmap's bit offset.

        Returns (count, start, end):
          count: total set bits
          start: logical bit offset of the first block with set bits, rounded
                 down to a 64-bit boundary (≥ 0)
          end:   logical bit offset past the last block with set bits, rounded
                 up to a 64-bit boundary (≤ _length)
        If count == 0, returns (0, 0, 0).
        """
        comptime width = simd_width_of[DType.uint8]()
        comptime t1_iters = 512 // width // 2
        comptime t1_bytes = 512
        comptime t2_iters = 64 // width

        if self._length == 0:
            return (0, 0, 0)

        ptr, total_bytes, lead_bits, trail_bits = self._aligned_byte_range()

        # first_byte / last_byte: byte offsets in the aligned buffer of the
        # first and last (exclusive) block that has a non-zero SIMD popcount.
        var first_byte = total_bytes  # sentinel: not yet found
        var last_byte = 0

        # Tier 1: 512-byte blocks, 2 interleaved uint8 accumulators.
        var t1_end = (total_bytes // t1_bytes) * t1_bytes
        var count = 0
        for i in range(0, t1_end, t1_bytes):
            var acc0 = SIMD[DType.uint8, width](0)
            var acc1 = SIMD[DType.uint8, width](0)
            comptime for j in range(t1_iters):
                acc0 += pop_count(
                    (ptr + i + (j * 2) * width).load[width=width]()
                )
                acc1 += pop_count(
                    (ptr + i + (j * 2 + 1) * width).load[width=width]()
                )
            var block_count = Int(
                (
                    acc0.cast[DType.uint16]() + acc1.cast[DType.uint16]()
                ).reduce_add()
            )
            if block_count > 0:
                if first_byte == total_bytes:
                    first_byte = i
                last_byte = i + t1_bytes
            count += block_count

        # Tier 2: 64-byte blocks for the remainder.
        for i in range(t1_end, total_bytes, 64):
            var acc = SIMD[DType.uint8, width](0)
            comptime for j in range(t2_iters):
                acc += pop_count((ptr + i + j * width).load[width=width]())
            var block_count = Int(acc.cast[DType.uint16]().reduce_add())
            if block_count > 0:
                if first_byte == total_bytes:
                    first_byte = i
                last_byte = i + 64
            count += block_count

        # Subtract bits outside [_offset, _offset + _length).
        if lead_bits:
            var lead_bytes = lead_bits >> 3
            var lead_sub_byte = lead_bits & 7
            for i in range(lead_bytes):
                count -= Int(pop_count(ptr[i]))
            if lead_sub_byte:
                count -= Int(
                    pop_count(ptr[lead_bytes] & UInt8((1 << lead_sub_byte) - 1))
                )
        if trail_bits:
            var trail_bytes = trail_bits >> 3
            var trail_sub_byte = trail_bits & 7
            var first_trail = total_bytes - trail_bytes
            if trail_sub_byte:
                count -= Int(
                    pop_count(ptr[first_trail - 1] >> UInt8(trail_sub_byte))
                )
            for i in range(first_trail, total_bytes):
                count -= Int(pop_count(ptr[i]))

        if count == 0:
            return (0, 0, 0)

        # Convert aligned-buffer byte offsets → logical bit positions, then
        # snap to 64-bit word boundaries (down for start, up for end).
        var start = max(0, first_byte * 8 - lead_bits)
        var end = min(self._length, last_byte * 8 - lead_bits)
        start = (start // 64) * 64
        end = min(self._length, ((end + 63) // 64) * 64)
        return (count, start, end)

    def count_set_bits(self) -> Int:
        """Count set bits in [_offset, _offset + _length)."""
        count, _, _ = self.count_set_bits_with_range()
        return count

    @always_inline
    def load_word(self, index: Int) -> UInt64:
        """Load 64 selection bits starting at logical position `index`.

        Handles the bitmap's bit offset correctly. Safe because Arrow buffers
        are 64-byte padded, so reading 8 bytes at any valid byte offset never
        faults.
        """
        var abs_pos = self._offset + index
        var byte_idx = abs_pos >> 3
        var bit_off = abs_pos & 7
        var raw = (
            (self._buffer.unsafe_ptr() + byte_idx)
            .bitcast[UInt64]()
            .load[alignment=1]()
        )
        return raw >> UInt64(bit_off)

    def slice(self, offset: Int, length: Int) -> Bitmap:
        """Return a zero-copy view of `length` bits starting at `offset`."""
        return Bitmap(self._buffer, self.bit_offset() + offset, length)

    def __eq__(self, other: Bitmap) -> Bool:
        """Return True if both bitmaps have identical logical bit patterns.

        Uses XOR + count_set_bits to correctly handle arbitrary bit offsets
        on either side. Returns False if either bitmap's buffer is device-resident.
        """
        if self._length != other._length:
            return False
        if not self._buffer.is_cpu() or not other._buffer.is_cpu():
            return False
        try:
            var xor_result = self ^ other
            return xor_result.count_set_bits() == 0
        except:
            return False

    # --- Bulk SIMD operations ---
    #
    # Arrow buffers are 64-byte aligned and zero-padded to multiples of 64 B.
    # simd_width_of[DType.uint8]() ∈ {16,32,64} all divide 64, so SIMD loops over ranges
    # returned by `_simd_byte_offset_range` terminate exactly on a cache-line boundary
    # with no overshoot.  Bits outside [_offset, _offset+_length) in results
    # are arbitrary; all operations that consume bitmaps respect _offset and _length.

    def __and__(self, other: Bitmap) raises -> Bitmap:
        """Return the bitwise AND of self and other."""
        return self._binop[_and](other)

    def __or__(self, other: Bitmap) raises -> Bitmap:
        """Return the bitwise OR of self and other."""
        return self._binop[_or](other)

    def __xor__(self, other: Bitmap) raises -> Bitmap:
        """Return the bitwise XOR of self and other."""
        return self._binop[_xor](other)

    def __invert__(self) -> Bitmap:
        """Return the bitwise NOT of this bitmap."""
        comptime width = simd_width_of[DType.uint8]()
        comptime assert 64 % width == 0
        comptime unroll = 64 // width

        src, total_bytes, lead_bits, _ = self._aligned_byte_range()

        var builder = BufferBuilder.alloc_uninit(total_bytes)
        var dst = builder.unsafe_ptr()
        for i in range(0, total_bytes, 64):
            comptime for j in range(unroll):
                comptime k = j * width
                (dst + i + k).store(~(src + i + k).load[width=width]())

        return Bitmap(builder.finish(), lead_bits, self._length)

    def and_not(self, other: Bitmap) raises -> Bitmap:
        """Return self & ~other  (A AND NOT B).

        Useful for null propagation: combine validity where *both* must be valid,
        and exclude elements that are null in `other`.
        """
        return self._binop[_and_not](other)

    @always_inline
    def _binop[
        op: def[W: Int](SIMD[DType.uint8, W], SIMD[DType.uint8, W]) -> SIMD[
            DType.uint8, W
        ]
    ](self, other: Bitmap) raises -> Bitmap:
        """Apply a byte-level binary operation across two bitmaps.

        Two code paths based on sub-byte alignment:
        - Same sub-byte offset: direct SIMD op, no bit shifting.
        - Different sub-byte offset: `other` is bit-shifted to align with `self`
          via overlapping loads: `(lo >> delta) | (hi << (8 - delta))`.

        src_a is backed up to its 64-byte boundary so src_a and dst are
        cache-line aligned.  src_b is shifted by the same lead byte offset to
        maintain byte correspondence.
        """
        if self._length != other._length:
            raise Error("Bitmap lengths must match")
        comptime width = simd_width_of[DType.uint8]()
        comptime assert 64 % width == 0
        comptime unroll = 64 // width

        src_a, total_bytes, lead_bits_a, _ = self._aligned_byte_range()
        ptr_b, _, lead_bits_b, _ = other._aligned_byte_range()

        var src_b = ptr_b + ((lead_bits_b >> 3) - (lead_bits_a >> 3))
        var builder = BufferBuilder.alloc_uninit(total_bytes)
        var dst = builder.unsafe_ptr()

        if lead_bits_a & 7 == lead_bits_b & 7:
            for i in range(0, total_bytes, 64):
                comptime for j in range(unroll):
                    comptime k = j * width
                    (dst + i + k).store(
                        op(
                            (src_a + i + k).load[width=width](),
                            (src_b + i + k).load[width=width](),
                        )
                    )
        else:
            var in_byte_shift = (lead_bits_b - lead_bits_a) & 7
            var rs = UInt8(in_byte_shift)
            var ls = UInt8(8 - in_byte_shift)
            for i in range(0, total_bytes, 64):
                comptime for j in range(unroll):
                    comptime k = j * width
                    var a = (src_a + i + k).load[width=width]()
                    var lo = (src_b + i + k).load[width=width]()
                    var hi = (src_b + i + k + 1).load[width=width]()
                    (dst + i + k).store(op(a, (lo >> rs) | (hi << ls)))

        return Bitmap(builder.finish(), lead_bits_a, self._length)

    def write_to[W: Writer](self, mut writer: W):
        writer.write(
            "Bitmap(offset=", self.bit_offset(), ", length=", self._length, ")"
        )

    def write_repr_to[W: Writer](self, mut writer: W):
        self.write_to(writer)


@always_inline
def _and[
    W: Int
](a: SIMD[DType.uint8, W], b: SIMD[DType.uint8, W]) -> SIMD[DType.uint8, W]:
    return a & b


@always_inline
def _or[
    W: Int
](a: SIMD[DType.uint8, W], b: SIMD[DType.uint8, W]) -> SIMD[DType.uint8, W]:
    return a | b


@always_inline
def _xor[
    W: Int
](a: SIMD[DType.uint8, W], b: SIMD[DType.uint8, W]) -> SIMD[DType.uint8, W]:
    return a ^ b


@always_inline
def _and_not[
    W: Int
](a: SIMD[DType.uint8, W], b: SIMD[DType.uint8, W]) -> SIMD[DType.uint8, W]:
    return a & ~b


# ---------------------------------------------------------------------------
# BitmapBuilder — mutable counterpart to Bitmap
# ---------------------------------------------------------------------------


struct BitmapBuilder(Movable):
    """Mutable bit-packed bitmap builder.

    Wraps `BufferBuilder` with bit-level write operations.  Call `finish(length)`
    to freeze into an immutable `Bitmap`.

    Example:
        var bm = BitmapBuilder.alloc(10)
        bm.set_bit(0, True)
        bm.set_bit(5, True)
        var bitmap = bm.finish(10)   # Bitmap of 10 bits, 2 set
    """

    var _builder: BufferBuilder

    def __init__(out self, var builder: BufferBuilder):
        self._builder = builder^

    @staticmethod
    def alloc(length: Int) -> BitmapBuilder:
        """Allocate a zero-filled builder for `length` bits."""
        return BitmapBuilder(BufferBuilder.alloc[DType.bool](length))

    @always_inline
    def unsafe_ptr(self) -> UnsafePointer[UInt8, MutExternalOrigin]:
        """Return the raw mutable byte pointer (for low-level bit operations).
        """
        return self._builder.ptr

    @always_inline
    def deposit_bits(mut self, bit_offset: Int, bits: UInt64, count: Int):
        """Deposit `count` LSBs from `bits` into the builder at `bit_offset`.

        The builder must be zero-filled (from `alloc`), as this uses OR to set
        bits. Handles arbitrary bit alignment — writes up to 9 bytes when the
        64-bit value straddles a byte boundary.
        """
        if count == 0:
            return
        var dst = self._builder.ptr
        var byte_idx = bit_offset >> 3
        var bit_off = bit_offset & 7
        var shifted = bits << UInt64(bit_off)
        # OR the low 8 bytes.
        var ptr64 = (dst + byte_idx).bitcast[UInt64]()
        ptr64.store[alignment=1](ptr64.load[alignment=1]() | shifted)
        # Handle overflow into the 9th byte when shifted bits spill past 64.
        if bit_off > 0 and bit_off + count > 64:
            dst[byte_idx + 8] = dst[byte_idx + 8] | UInt8(
                bits >> UInt64(64 - bit_off)
            )

    # TODO: add safe apis
    @always_inline
    def set_bit(mut self, index: Int, value: Bool):
        """Set or clear the bit at `index`."""
        self._builder.unsafe_set[DType.bool](index, value)

    def set_range(mut self, start: Int, length: Int, value: Bool):
        """Set `length` bits starting at `start` to `value`.

        Handles byte-aligned bulk fills via `memset` for the middle bytes,
        with partial masks at the boundaries.
        """
        if length == 0:
            return
        var ptr = self._builder.ptr
        var end = start + length
        var start_byte = start >> 3
        var start_bit = start & 7
        var end_byte = end >> 3
        var end_bit = end & 7

        var fill = UInt8(255 if value else 0)

        if start_byte == end_byte:
            # All bits in one byte
            var mask = UInt8((1 << end_bit) - 1) & (
                UInt8(0xFF) << UInt8(start_bit)
            )
            if value:
                ptr[start_byte] = ptr[start_byte] | mask
            else:
                ptr[start_byte] = ptr[start_byte] & ~mask
            return

        # Leading partial byte
        if start_bit != 0:
            var mask = UInt8(0xFF) << UInt8(start_bit)
            if value:
                ptr[start_byte] = ptr[start_byte] | mask
            else:
                ptr[start_byte] = ptr[start_byte] & ~mask
            start_byte += 1

        # Trailing partial byte
        if end_bit != 0:
            var mask = UInt8((1 << end_bit) - 1)
            if value:
                ptr[end_byte] = ptr[end_byte] | mask
            else:
                ptr[end_byte] = ptr[end_byte] & ~mask

        # Full middle bytes
        if end_byte > start_byte:
            memset(ptr + start_byte, fill, end_byte - start_byte)

    def copy_bits(
        mut self,
        src_ptr: UnsafePointer[UInt8, _],
        src_offset: Int,
        dst_offset: Int,
        length: Int,
    ):
        """Bulk-copy `length` bits from `src_ptr` at bit `src_offset` into
        self at bit `dst_offset`.

        Three code paths ordered by expected frequency:
        1. Same sub-byte alignment: memcpy for middle bytes, masks at edges.
        2. Different alignment: shift-and-merge byte-by-byte.
        3. Short runs (< 16 bits): bit-by-bit fallback to avoid setup overhead.
        """
        if length == 0:
            return
        var dst = self._builder.ptr

        # Short runs: bit-by-bit is faster than computing byte masks.
        if length < 16:
            for i in range(length):
                var s_byte = (src_offset + i) >> 3
                var s_bit = (src_offset + i) & 7
                var val = (src_ptr[s_byte] >> UInt8(s_bit)) & 1
                var d_byte = (dst_offset + i) >> 3
                var d_bit = (dst_offset + i) & 7
                var d_mask = UInt8(1 << d_bit)
                if val:
                    dst[d_byte] = dst[d_byte] | d_mask
                else:
                    dst[d_byte] = dst[d_byte] & ~d_mask
            return

        var src_bit = src_offset & 7
        var dst_bit = dst_offset & 7

        if src_bit == dst_bit:
            # Same sub-byte alignment: can use memcpy for the bulk.
            var src_byte = src_offset >> 3
            var dst_byte = dst_offset >> 3
            var end_bit = dst_offset + length
            var end_byte = end_bit >> 3
            var end_sub = end_bit & 7

            if dst_bit != 0:
                # Merge leading partial byte.
                var keep_mask = UInt8((1 << dst_bit) - 1)
                dst[dst_byte] = (dst[dst_byte] & keep_mask) | (
                    src_ptr[src_byte] & ~keep_mask
                )
                src_byte += 1
                dst_byte += 1

            # Full middle bytes via memcpy.
            if end_byte > dst_byte:
                memcpy(
                    dest=dst + dst_byte,
                    src=src_ptr + src_byte,
                    count=end_byte - dst_byte,
                )

            if end_sub != 0:
                # Merge trailing partial byte.
                var trail_byte_src = src_byte + (end_byte - dst_byte)
                var keep_mask = UInt8(0xFF) << UInt8(end_sub)
                dst[end_byte] = (dst[end_byte] & keep_mask) | (
                    src_ptr[trail_byte_src] & ~keep_mask
                )
        else:
            # Different sub-byte alignment: shift-and-merge byte-by-byte.
            var src_byte = src_offset >> 3
            var dst_byte_start = dst_offset >> 3
            var end_bit = dst_offset + length
            var end_byte = end_bit >> 3
            var end_sub = end_bit & 7
            var delta = src_bit - dst_bit

            if dst_bit != 0:
                # Leading partial byte.
                var keep_mask = UInt8((1 << dst_bit) - 1)
                var shifted: UInt8
                if delta > 0:
                    shifted = (src_ptr[src_byte] >> UInt8(delta)) | (
                        src_ptr[src_byte + 1] << UInt8(8 - delta)
                    )
                else:
                    shifted = src_ptr[src_byte] << UInt8(-delta)
                    if src_byte > 0:
                        shifted |= src_ptr[src_byte - 1] >> UInt8(8 + delta)
                dst[dst_byte_start] = (dst[dst_byte_start] & keep_mask) | (
                    shifted & ~keep_mask
                )
                dst_byte_start += 1

            # Full middle bytes.
            var src_bit_pos = src_offset + ((dst_byte_start << 3) - dst_offset)
            for j in range(dst_byte_start, end_byte):
                var sb = src_bit_pos >> 3
                var so = src_bit_pos & 7
                if so == 0:
                    dst[j] = src_ptr[sb]
                else:
                    dst[j] = (src_ptr[sb] >> UInt8(so)) | (
                        src_ptr[sb + 1] << UInt8(8 - so)
                    )
                src_bit_pos += 8

            if end_sub != 0:
                # Trailing partial byte.
                var sb = src_bit_pos >> 3
                var so = src_bit_pos & 7
                var shifted: UInt8
                if so == 0:
                    shifted = src_ptr[sb]
                else:
                    shifted = (src_ptr[sb] >> UInt8(so)) | (
                        src_ptr[sb + 1] << UInt8(8 - so)
                    )
                var keep_mask = UInt8(0xFF) << UInt8(end_sub)
                dst[end_byte] = (dst[end_byte] & keep_mask) | (
                    shifted & ~keep_mask
                )

    def extend(mut self, src: Bitmap, dst_start: Int, length: Int):
        """Copy `length` bits from `src` (from its `_offset`) into self at `dst_start`.

        Replaces the `bitmap_extend` free function.
        """
        self.copy_bits(
            src._buffer.unsafe_ptr(), src.bit_offset(), dst_start, length
        )

    def resize(mut self, capacity: Int) raises:
        """Resize the underlying buffer to hold `capacity` bits."""
        self._builder.resize[DType.bool](capacity)

    def finish(mut self, length: Int) -> Bitmap:
        """Freeze the builder into an immutable `Bitmap` of `length` bits.

        The builder is reset to empty and can be reused after this call.
        """
        return Bitmap(self._builder.finish(), 0, length)
